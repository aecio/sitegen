plugins {
    id 'java'
    id 'application'
    id("com.diffplug.spotless") version "8.0.0"
}

version = '0.5.0'

java {
    sourceCompatibility = JavaVersion.VERSION_11
}

application {
    mainClass = "com.aeciosantos.sitegen.Main"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'com.github.spullara.mustache.java', name: 'compiler', version: '0.9.14'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.20.0'
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.20.0'
    implementation group: 'commons-io', name: 'commons-io', version: '2.20.0'
    implementation group: 'io.undertow', name: 'undertow-core', version: '2.3.20.Final'
    implementation group: 'com.google.guava', name: 'guava', version: '33.5.0-jre'
    implementation group: 'org.commonmark', name: 'commonmark', version: '0.27.0'
    implementation group: 'org.freemarker', name: 'freemarker', version: '2.3.34'
    implementation group: 'info.picocli', name: 'picocli', version: '4.7.7'
}

test {
    systemProperties 'property': 'value'
}

//
// Adds version to final JAR artifact
//
jar {
    manifest {
        attributes(
                'Implementation-Title': 'Sitegen',
                'Implementation-Version': archiveVersion
        )
    }
}

// Setup for spotless plugin for code formatting
// To format code, use:
//     gradlew spotlessApply
// To verify code format compliance, use:
//      gradle spotlessCheck
spotless {
    format 'misc', {
        // define the files to apply `misc` to
        target '*.gradle', '.gitattributes', '.gitignore'

        // define the steps to apply to those files
        trimTrailingWhitespace()
        leadingTabsToSpaces(2)
        endWithNewline()
    }

    java {
        // don't need to set target, it is inferred from java

        // fix imports
        removeUnusedImports()

        // apply a specific flavor of google-java-format
        googleJavaFormat('1.28.0')

        // fix formatting of type annotations
        formatAnnotations()
    }
}
